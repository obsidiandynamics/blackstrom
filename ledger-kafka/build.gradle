def packageName = "blackstrom-ledger-kafka"
def packageDescription = "Kafka-based ledger implementation"

dependencies {
  api project(":")
  api "com.obsidiandynamics.fulcrum:fulcrum-retry:${fulcrumVersion}"
  api "com.obsidiandynamics.jackdaw:jackdaw-core:${jackdawVersion}"
  compileOnly "org.apache.kafka:kafka-clients:${kafkaVersion}"
  
  testImplementation project(":blackstrom-codec-jackson")
  testImplementation project(":blackstrom-codec-jackson").sourceSets.test.output
  testImplementation project(":blackstrom-codec-kryo")
  testImplementation project(":blackstrom-codec-kryo").sourceSets.test.output
  testImplementation project(":blackstrom-sim")
  testImplementation project(":blackstrom-sim").sourceSets.test.output
  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-docker-compose:${fulcrumVersion}"
  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-fslock:${fulcrumVersion}"
  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-jgroups:${fulcrumVersion}"
  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-select:${fulcrumVersion}"
  testImplementation "com.obsidiandynamics.jackdaw:jackdaw-assurance:${jackdawVersion}"
  testImplementation "org.jgroups:jgroups:${jgroupsVersion}"
  testImplementation "org.apache.kafka:kafka-clients:${kafkaVersion}"

  testImplementation "com.obsidiandynamics.fulcrum:fulcrum-launcher:${fulcrumVersion}"
}

task launch(dependsOn: [classes, testClasses]) {
  doLast {
    def launcherClasses = new ArrayList()
    launcherClasses += "com.obsidiandynamics.blackstrom.bank.KafkaKryoRandomBankTransferIT"
    launcherClasses += "com.obsidiandynamics.blackstrom.bank.KafkaJacksonRandomBankTransferIT"
    launcherClasses += "com.obsidiandynamics.blackstrom.ledger.KafkaKryoLedgerIT"
    launcherClasses += "com.obsidiandynamics.blackstrom.ledger.KafkaJacksonLedgerIT"
    
    def perfJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " +
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = sourceSets.main.compileClasspath + sourceSets.main.runtimeClasspath + sourceSets.test.compileClasspath + sourceSets.test.runtimeClasspath
      mainClass = "com.obsidiandynamics.launcher.Launcher"
      args = launcherClasses
      jvmArgs = Arrays.asList(perfJvmArgs.split(" ")) + baseJvmArgs
      standardInput = System.in
    }
  }
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      artifactId = packageName
      from components.java
      pom {
        name = packageName
        description = packageDescription
        url = 'https://github.com/obsidiandynamics/blackstrom'
        licenses {
          license {
            name = 'BSD 3-Clause License'
            url = 'https://opensource.org/licenses/BSD-3-Clause'
          }
        }
        developers {
          developer {
            id = 'ekoutanov'
            name = 'Emil Koutanov'
            email = 'ekoutanov@gmail.com'
          }
        }
        scm {
          connection = 'scm:git:git://github.com/obsidiandynamics/blackstrom.git'
          developerConnection = 'scm:git:ssh://github.com/obsidiandynamics/blackstrom.git'
          url = 'https://github.com/obsidiandynamics/blackstrom'
        }
      }
    }
  }
}

task testJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
  mergeServiceFiles()
  archiveClassifier.set('tests')
  from sourceSets.main.output
  from sourceSets.test.output
  configurations = [project.configurations.api, project.configurations.testImplementation]
}
