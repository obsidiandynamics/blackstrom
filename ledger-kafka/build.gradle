def packageName = "blackstrom-ledger-kafka"
version = project(":").version

dependencies {
  compile project(":")
  compile "com.obsidiandynamics.fulcrum:fulcrum-format:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-retry:${fulcrumVersion}"
  compile "com.obsidiandynamics.jackdaw:jackdaw-core:${jackdawVersion}"
  compile "org.apache.kafka:kafka-clients:${kafkaVersion}"
  
  testCompile project(":blackstrom-codec-jackson")
  testCompile project(":blackstrom-codec-jackson").sourceSets.test.output
  testCompile project(":blackstrom-codec-kryo")
  testCompile project(":blackstrom-codec-kryo").sourceSets.test.output
  testCompile project(":blackstrom-sim")
  testCompile project(":blackstrom-sim").sourceSets.test.output
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-docker-compose:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-jgroups:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-random:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-select:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.jackdaw:jackdaw-assurance:${jackdawVersion}"
  testCompile "org.jgroups:jgroups:${jgroupsVersion}"
  
  testRuntime "com.obsidiandynamics.fulcrum:fulcrum-launcher:${fulcrumVersion}"
}

task launch(dependsOn: testClasses) {
  doLast {
    def launcherClasses = new ArrayList()
    launcherClasses += "com.obsidiandynamics.blackstrom.bank.KafkaKryoRandomBankTransferIT"
    launcherClasses += "com.obsidiandynamics.blackstrom.bank.KafkaJacksonRandomBankTransferIT"
    launcherClasses += "com.obsidiandynamics.blackstrom.ledger.KafkaKryoLedgerIT"
    launcherClasses += "com.obsidiandynamics.blackstrom.ledger.KafkaJacksonLedgerIT"
    
    def perfJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-XX:+UseBiasedLocking -Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " + 
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = sourceSets.test.runtimeClasspath
      main = "com.obsidiandynamics.launcher.Launcher"
      args = launcherClasses
      jvmArgs = Arrays.asList(perfJvmArgs.split(" ")) + baseJvmArgs
      standardInput = System.in
    }
  }
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact jar
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
def packageDesc = "Kafka-based ledger implementation"
def repoName = "blackstrom"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = true
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}

task testJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
  mergeServiceFiles()
  classifier = 'tests'
  from sourceSets.main.output
  from sourceSets.test.output
  configurations = [project.configurations.compile, project.configurations.runtime,
                    project.configurations.testCompile, project.configurations.testRuntime]
}
