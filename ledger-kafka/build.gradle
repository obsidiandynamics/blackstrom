def packageName = "blackstrom-ledger-kafka"
version = project(":").version
  
ext {
  jackdawVersion = "0.3.0"
}
  
dependencies {
  compile project(":")
  compile "com.obsidiandynamics.fulcrum:fulcrum-retry:${fulcrumVersion}"
  compile "com.obsidiandynamics.jackdaw:jackdaw-core:${jackdawVersion}"
  compile "org.apache.kafka:kafka-clients:1.1.0"
  
  testCompile project(":blackstrom-codec-jackson")
  testCompile project(":blackstrom-codec-jackson").sourceSets.test.output
  testCompile project(":blackstrom-codec-kryo")
  testCompile project(":blackstrom-codec-kryo").sourceSets.test.output
  testCompile project(":blackstrom-sim")
  testCompile project(":blackstrom-sim").sourceSets.test.output
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-docker-compose:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-jgroups:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.fulcrum:fulcrum-select:${fulcrumVersion}"
  testCompile "com.obsidiandynamics.jackdaw:jackdaw-assurance:${jackdawVersion}"
}

task addLauncherApps() {
  project(":").launcherClasspath += sourceSets.main.runtimeClasspath + sourceSets.test.runtimeClasspath
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.bank.KafkaKryoRandomBankTransferIT"
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.bank.KafkaJacksonRandomBankTransferIT"
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.ledger.KafkaKryoLedgerIT"
  project(":").launcherClasses += "com.obsidiandynamics.blackstrom.ledger.KafkaJacksonLedgerIT"
}

jar {
  baseName packageName
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
  archives javadocJar
  archives sourcesJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      groupId project(":").group
      artifactId packageName
      version project(":").version
      from components.java
      artifact sourcesJar
      artifact javadocJar
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
def packageDesc = "Kafka-based ledger implementation"
def repoName = "blackstrom"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = true
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}

task testJar(type: com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar) {
  mergeServiceFiles()
  classifier = 'tests'
  from sourceSets.main.output
  from sourceSets.test.output
  configurations = [project.configurations.compile, project.configurations.runtime,
                    project.configurations.testCompile, project.configurations.testRuntime]
}