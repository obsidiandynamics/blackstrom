plugins {
  id "com.jfrog.bintray" version "1.8.4"
  id "com.github.johnrengelman.shadow" version "5.0.0"
  id "com.github.ben-manes.versions" version "0.21.0"
}
apply plugin: "maven-publish"
apply plugin: "com.jfrog.bintray"

group = "com.obsidiandynamics.blackstrom"
version = "0.58.0-SNAPSHOT"
  
def envUser = "BINTRAY_USER"
def envKey = "BINTRAY_KEY"
task bintrayCredentialsCheck {
  doLast {
    if (System.getenv(envUser) == null) {
      throw new GradleException("No Bintray username specified; set with 'export ${envUser}=<username>'")
    }
    if (System.getenv(envKey) == null) {
      throw new GradleException("No Bintray key specified; set with 'export ${envKey}=<key>'")
    }
  }
}

def baseJvmArgs = [
  "--add-opens=java.base/java.io=ALL-UNNAMED",
  "--add-opens=java.base/java.lang=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.invoke=ALL-UNNAMED",
  "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
  "--add-opens=java.base/java.nio=ALL-UNNAMED",
  "--add-opens=java.base/java.util=ALL-UNNAMED",
  "--add-opens=java.base/java.util.concurrent.atomic=ALL-UNNAMED",
  "--add-opens=java.base/sun.nio.ch=ALL-UNNAMED",
  "-XX:-MaxFDLimit"
]
project.ext.set("baseJvmArgs", baseJvmArgs)

def getBlackstromSystemProperties() {
  def filteredProps = new Properties()
  for (def propKey : System.getProperties().stringPropertyNames()) {
    if (propKey.startsWith("blackstrom.")) {
      filteredProps.setProperty(propKey, System.getProperties().getProperty(propKey))
    }
  }
  return filteredProps
}

allprojects {
  apply plugin: "java"
  apply plugin: "jacoco"
  apply plugin: "maven-publish"
  apply plugin: "com.jfrog.bintray"
  apply plugin: "com.github.johnrengelman.shadow"

  sourceCompatibility = 11
  targetCompatibility = 11

  repositories {
    jcenter()
    maven {
      url "https://oss.sonatype.org/content/repositories/snapshots"
    }
  }
  
  ext {
    assertjVersion = "3.12.2"
    commonsLangVersion = "3.9"
    equalsverifierVersion = "3.1.9"
    fulcrumVersion = "0.36.0"
    hazelcastVersion = "3.12"
    hdrHistogramVersion = "2.1.11"
    jackdawVersion = "1.33.0"
    jacksonVersion = "2.9.9"
    jgroupsVersion = "4.1.0.Final"
    junitVersion = "4.12"
    log4jVersion = "1.2.17"
    kafkaVersion = "2.2.1"
    kryoVersion = "5.0.0-RC4"
    meteorVersion = "0.24.0"
    mockitoVersion = "2.28.2"
    pojotesterVersion = "0.9.0"
    slf4jVersion = "1.7.25"
    yconfVersion = "0.20.0"
    zerologVersion = "0.30.0"
  }
  
  dependencies {
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-assert:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-await:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-junit:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-mockito:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-testmark:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-threads:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.fulcrum:fulcrum-verifier:${fulcrumVersion}"
    testCompile "com.obsidiandynamics.yconf:yconf-snakeyaml:${yconfVersion}"
    testCompile "junit:junit:${junitVersion}"
    testCompile "org.mockito:mockito-core:${mockitoVersion}"
    testCompile "org.hdrhistogram:HdrHistogram:${hdrHistogramVersion}"
    testCompile "nl.jqno.equalsverifier:equalsverifier:${equalsverifierVersion}"
    testCompile "com.obsidiandynamics.pojotester:pojotester:${pojotesterVersion}"
    testCompile "org.assertj:assertj-core:${assertjVersion}"

    testRuntime "org.slf4j:slf4j-log4j12:${slf4jVersion}"
    testRuntime "log4j:log4j:${log4jVersion}"
  }
    
  compileJava {
    options.compilerArgs += "-proc:none"
    options.compilerArgs += "-Xlint:unchecked"
  }

  compileTestJava {
    options.compilerArgs += "-proc:none"
    options.compilerArgs += "-Xlint:unchecked"
    options.compilerArgs += "-Xlint:deprecation"
  }
  
  tasks.withType(JavaCompile) {
    options.fork = true
    options.incremental = true
  }
  
  test {
    exclude "**/*IT.class"
    jvmArgs += baseJvmArgs
    systemProperties getBlackstromSystemProperties()
  }
  
  task integrationTest(type: Test, description: "Runs integration tests", group: "Verification") {
    include "**/*IT.class"
    jvmArgs += baseJvmArgs
    systemProperties getBlackstromSystemProperties()
  }

  jacoco {
    toolVersion = "0.8.4"
  }
  
  task verify {  
    dependsOn test
    dependsOn integrationTest
  }

  jacocoTestReport {
    additionalSourceDirs.from = files(sourceSets.main.allSource.srcDirs)
    sourceDirectories.from = files(sourceSets.main.allSource.srcDirs)
    classDirectories.from = files(sourceSets.main.output)
    executionData.from = files("$buildDir/jacoco/test.exec") + files("$buildDir/jacoco/integrationTest.exec")
    reports {
      html.enabled true
      xml.enabled true
      csv.enabled false
    }
  }

  // invoke this task when ready to publish to Bintray
  bintrayUpload {
    dependsOn ":bintrayCredentialsCheck"
    dependsOn "jar"
    dependsOn "sourcesJar"
    dependsOn "javadocJar"
    dependsOn "generatePomFileForMavenJavaPublication"
  }
}

subprojects {
  dependencies {
    testCompile project(":").sourceSets.test.output
  }
}

task jacocoMerge(type: JacocoMerge) {
  mustRunAfter = allprojects.test + allprojects.integrationTest
  executionData = files(allprojects.jacocoTestReport.executionData)
  doFirst {
    executionData = files(files(allprojects.jacocoTestReport.executionData).findAll { it.exists() })
  }
}

task jacocoRootReport(type: JacocoReport) {
  dependsOn jacocoMerge
  mustRunAfter = allprojects.test + allprojects.integrationTest
  additionalSourceDirs.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  sourceDirectories.from = files(allprojects.sourceSets.main.allSource.srcDirs)
  classDirectories.from = files(allprojects.sourceSets.main.output)
  executionData jacocoMerge.destinationFile
  reports {
    html.enabled true
    xml.enabled true
    csv.enabled false
  }
  onlyIf = {
    true
  }
  afterEvaluate {
    classDirectories.from = files(classDirectories.files.collect {
      fileTree(dir: it, exclude: ["sample/**", "**/*Uncovered*", "**/generated/**"])
    })
  }
}

def packageName = "blackstrom-core"

dependencies {
  compile "com.obsidiandynamics.fulcrum:fulcrum-concat:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-flow:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-format:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-func:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-nanoclock:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-nodequeue:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-props:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-random:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-scheduler:${fulcrumVersion}"
  compile "com.obsidiandynamics.fulcrum:fulcrum-worker:${fulcrumVersion}"
  compile "com.obsidiandynamics.yconf:yconf-core:${yconfVersion}"
  compile "com.obsidiandynamics.zerolog:zerolog-core:${zerologVersion}"
  compile "org.apache.commons:commons-lang3:${commonsLangVersion}"
  runtime "com.obsidiandynamics.zerolog:zerolog-slf4j17:${zerologVersion}"
  runtime "org.slf4j:slf4j-api:${slf4jVersion}"
  
  testRuntime "com.obsidiandynamics.fulcrum:fulcrum-launcher:${fulcrumVersion}"
}

task launch(dependsOn: testClasses) {
  doLast {
    def launcherClasses = new ArrayList()
    launcherClasses += "com.obsidiandynamics.blackstrom.ledger.MultiNodeQueueLedgerTest"
    
    def perfJvmArgs = "-XX:-MaxFDLimit -XX:+TieredCompilation -XX:+UseNUMA -XX:+UseCondCardMark " + 
                      "-XX:+UseBiasedLocking -Xms2G -Xmx2G -Xss1M -XX:+UseG1GC -XX:MaxGCPauseMillis=200 " + 
                      "-XX:InitiatingHeapOccupancyPercent=0 -Djava.net.preferIPv4Stack=true " + 
                      "-Dlauncher.package.compress.level=3"
    javaexec {
      systemProperties = System.properties
      classpath = sourceSets.test.runtimeClasspath
      main = "com.obsidiandynamics.launcher.Launcher"
      args = launcherClasses
      jvmArgs = Arrays.asList(perfJvmArgs.split(" ")) + baseJvmArgs
      standardInput = System.in
    }
  }
}

jar {
  baseName packageName
  finalizedBy jacocoRootReport
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  baseName packageName
  classifier = "javadoc"
  from "$buildDir/docs/javadoc"
}
 
task sourcesJar(type: Jar) {
  baseName packageName
  from sourceSets.main.allSource
  classifier = "sources"
}
 
artifacts {
  archives jar
}

publishing {
  publications {
	mavenJava(MavenPublication) {
      from components.java
      afterEvaluate {
        groupId project(":").group
        artifactId packageName
        version project(":").version
        artifact jar
        artifact sourcesJar
        artifact javadocJar
      }
    }
  }
  repositories {
    maven {
      url "${project(':').projectDir}/../repo"
    }
  }
}

def packageDesc = "Transactional event fabric for the JVM"
def repoName = "blackstrom"
bintray {
  user = System.getenv(envUser)
  key = System.getenv(envKey)
  publications = ["mavenJava"]
  pkg {
    repo = "${repoName}"
    name = packageName
    userOrg = "obsidiandynamics"
    desc = packageDesc
    websiteUrl = "https://github.com/obsidiandynamics/${repoName}"
    licenses = ["BSD New"]
    vcsUrl = "https://github.com/obsidiandynamics/${repoName}"
    issueTrackerUrl = "https://github.com/obsidiandynamics/${repoName}/issues"
    publicDownloadNumbers = true
    githubRepo = "obsidiandynamics/${repoName}"
    override = true
    publish = true
    version {
      name = project(":").version
      desc = packageDesc
      released  = new Date()
      vcsTag = project(":").version
    }
  }
}
